plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.30"
}

def getVersionFromGit = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        def version = stdout.toString().trim()
        def nexusVersion = version.replaceAll("-rc.*", "-SNAPSHOT")
        if (nexusVersion.startsWith("v")) {
          nexusVersion = nexusVersion.replaceAll("v", "")
        }
        println "The version is: "+ nexusVersion
        return nexusVersion
    }
    catch (e) {
        throw RuntimeException(e)
    }
}

group = 'io.github.shyamz-22'
version = getVersionFromGit()

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

compileKotlin {
  kotlinOptions {
       jvmTarget = "1.8"
       apiVersion = "1.2"
       languageVersion = "1.2"
       verbose = true
       freeCompilerArgs = ["-include-runtime", "-Xskip-runtime-version-check"]
   }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


ext.uniRestVersion = '1.4.9'
ext.assertjCoreVersion = '3.9.0'
ext.jacksonVersion = '2.9.3'
ext.servletApiVersion = '4.0.0'
ext.wireMockVersion = '2.14.0'
ext.jwtLibVersion = '5.4'
ext.caffeineVersion = '2.6.1'
ext.kotlin_version = '1.2.30'

repositories {
    mavenCentral()
}

dependencies {

    compile group: "com.mashape.unirest",           name: "unirest-java",          version: "$uniRestVersion"
    compile group: "com.nimbusds",                  name: "nimbus-jose-jwt",       version: "$jwtLibVersion"
    compile group: "com.github.ben-manes.caffeine", name:"caffeine",               version: "$caffeineVersion"
    compile group: "com.fasterxml.jackson.core",    name: "jackson-core",          version: "$jacksonVersion"
    compile group: "com.fasterxml.jackson.module",  name: "jackson-module-kotlin", version: "$jacksonVersion"


    compile group: "org.jetbrains.kotlin",         name: "kotlin-stdlib-jdk8",    version: "$kotlin_version"

    compileOnly group: "javax.servlet",                 name: "javax.servlet-api",     version: "$servletApiVersion"

    testCompile group: "javax.servlet",          name: "javax.servlet-api",   version: "$servletApiVersion"
    testCompile group: "org.assertj",            name: "assertj-core",        version: "$assertjCoreVersion"
    testCompile group: "com.github.tomakehurst", name: "wiremock",            version: "$wireMockVersion"
    testCompile group: "org.mockito",            name: "mockito-all",         version: "1.10.19"
    testCompile group: "junit",                  name: "junit",               version: "4.12"

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

check.dependsOn jacocoTestReport

apply from: 'publishToMaven.gradle'
