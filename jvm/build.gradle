def getVersionFromGit = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        def version = stdout.toString().trim()
        def nexusVersion = version.replaceAll("-rc.*", "-SNAPSHOT")
        if (nexusVersion.startsWith("v")) {
          nexusVersion = nexusVersion.replaceAll("v", "")
        }
        println "The version is: "+ nexusVersion
        return nexusVersion
    }
    catch (e) {
        throw RuntimeException(e)
    }
}

group = 'io.github.shyamz-22'
version = getVersionFromGit()

buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


ext.uniRestVersion = '1.4.9'
ext.assertjCoreVersion = '3.9.0'
ext.jacksonVersion = '2.9.3'
ext.servletApiVersion = '4.0.0'
ext.wireMockVersion = '2.14.0'
ext.jwtLibVersion = '5.4'
ext.caffeineVersion = '2.6.1'



dependencies {

    compile group: "com.mashape.unirest", name: "unirest-java", version: "$uniRestVersion"
    compile group: "com.fasterxml.jackson.core", name: "jackson-core", version: "$jacksonVersion"
    compile group: "com.fasterxml.jackson.module", name: "jackson-module-kotlin", version: "$jacksonVersion"
    compile group: "com.nimbusds", name: "nimbus-jose-jwt", version: "$jwtLibVersion"


    compile "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"

    compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "$servletApiVersion"
    testCompile group: "javax.servlet", name: "javax.servlet-api", version: "$servletApiVersion"


    testCompile group: "org.assertj", name: "assertj-core", version: "$assertjCoreVersion"
    testCompile group: "com.github.tomakehurst", name: "wiremock", version: "$wireMockVersion"
    testCompile group: "org.mockito", name: "mockito-all", version: "1.10.19"
    testCompile group: "junit", name: "junit", version: "4.12"

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

apply from: 'publishToMaven.gradle'
